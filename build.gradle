plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    compileJava.options.encoding('UTF-8')
    withJavadocJar()
    withSourcesJar()
}

group = 'com.github.romanqed'
archivesBaseName = 'jda-commands'
version = System.getenv('RELEASE_VERSION') ?: '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    // JDA
    implementation group: 'net.dv8tion', name: 'JDA', version: '5.0.0-alpha.20'
    // Annotation processor
    api group: 'org.atteo.classindex', name: 'classindex', version: '3.11'
    annotationProcessor group: 'org.atteo.classindex', name: 'classindex', version: '3.11'
    // Jeflect and other utils
    api group: 'com.github.romanqed', name: 'jeflect', version: '3.0.0'
    api group: 'com.github.romanqed', name: 'java-utils', version: '1.3.0'
}

jar {
    from sourceSets.main.output
    from sourceSets.main.allJava
}

signing {
    sign publishing.publications
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            // Specify artifacts
            artifactId = archivesBaseName
            groupId = group
            version = version
            from components.java
            // Configure pom
            pom {
                name.set(archivesBaseName)
                description.set('A small library that implements a command system for JDA. Uses jeflect.')
                url.set('https://github.com/RomanQed/jda-commands')

                organization {
                    name.set('com.github.romanqed')
                    url.set('https://github.com/RomanQed')
                }

                issueManagement {
                    system.set('GitHub')
                    url.set('https://github.com/RomanQed/jda-commands/issues')
                }

                licenses {
                    license {
                        name.set('Apache License 2.0')
                        url.set('https://github.com/RomanQed/jda-commands/blob/main/LICENSE')
                    }
                }

                scm {
                    url.set('https://github.com/RomanQed/jda-commands')
                    connection.set('scm:https://github.com/RomanQed/jda-commands.git')
                    developerConnection.set('scm:https://github.com/RomanQed/jda-commands.git')
                }

                developers {
                    developer {
                        id.set('RomanQed')
                        name.set('Roman Bakaldin')
                        email.set('gbakaldin@gmail.com')
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username System.getenv('SONATYPE_USERNAME')
                password System.getenv('SONATYPE_PASSWORD')
            }
        }
    }
}